# Process this file with autoconf to produce a configure script.
# $Id: configure.in,v 1.25 2003/03/11 07:23:52 brendan Exp $

m4_define(libshout_major, 2)
m4_define(libshout_minor, 0)
m4_define(libshout_micro, 0)
m4_define(libshout_version,
  m4_if(libshout_micro, 0, libshout_major.libshout_minor,
   libshout_major.libshout_minor.libshout_micro))

AC_INIT([libshout], libshout_version, [icecast@xiph.org])
AC_PREREQ(2.52)
AC_CONFIG_SRCDIR([src/shout.c])
AM_CONFIG_HEADER([config.h])

AC_DEFINE([LIBSHOUT_MAJOR], libshout_major, ["Shout library major version"])
AC_DEFINE([LIBSHOUT_MINOR], libshout_minor, ["Shout library minor version"])
AC_DEFINE([LIBSHOUT_MICRO], libshout_micro, ["Shout library patch version"])

VERSION=libshout_version

AM_INIT_AUTOMAKE([libshout], libshout_version)
AM_MAINTAINER_MODE

AC_PROG_CC
AM_PROG_LIBTOOL

dnl Set some options based on environment

if test -z "$GCC"; then
        case $host in 
        *-*-irix*)
                DEBUG="-g -signed" 
                CFLAGS="-O2 -w -signed"
                PROFILE="-p -g3 -O2 -signed"
		;;
        sparc-sun-solaris*)
                DEBUG="-v -g"
                CFLAGS="-xO4 -fast -w -fsimple -native -xcg92"
                PROFILE="-v -xpg -g -xO4 -fast -native -fsimple -xcg92 -Dsuncc"
		;;
        *)
                DEBUG="-g"
                CFLAGS="-O"
                PROFILE="-g -p" 
		;;
        esac
else
        XIPH_CFLAGS="-Wall -ffast-math -fsigned-char"
        AC_DEFINE(_GNU_SOURCE, ,[Define if you have POSIX and GNU specifications])
        DEBUG="-g"
        PROFILE="-pg -g"
fi

dnl Checks for programs.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([inttypes.h])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Check for types
AC_CHECK_TYPE([uint32_t],
  [AC_DEFINE(HAVE_C99_INTTYPES, 1, [Define if you have the C99 integer types])],
  [AC_CHECK_SIZEOF(short)
   AC_CHECK_SIZEOF(int)
   AC_CHECK_SIZEOF(long)
   AC_CHECK_SIZEOF(long long)])
AC_CHECK_TYPES([socklen_t])
AH_VERBATIM([X_HAVE_SOCKLEN_T],
  [#ifndef HAVE_SOCKLEN_T
typedef int socklen_t;
#endif])

dnl Checks for library functions.
AC_CHECK_FUNCS(nanosleep)
AC_CHECK_FUNCS(sethostent endhostent)
AC_CHECK_FUNCS(getnameinfo getaddrinfo inet_pton)

dnl Extra dependencies

AC_ARG_ENABLE([pthread],
  [[  --disable-pthread       do not link with pthread even if it is available]])

have_thread="no"
if test "$enable_pthread" != "no"
then
  ACX_PTHREAD([
    LIBS="$LIBS $PTHREAD_LIBS"
    XIPH_CFLAGS="$XIPH_CFLAGS $PTHREAD_CFLAGS"
    CC="$PTHREAD_CC"
    have_thread="yes"
    ])
fi
AM_CONDITIONAL([HAVE_THREAD], [test "$have_thread" == "yes"])
if test "$have_thread" != "yes"
then
  AC_DEFINE([NO_THREAD], 1, [Define if you don't want to use the thread library])
fi

XIPH_PATH_OGG
XIPH_PATH_VORBIS
XIPH_CFLAGS="$XIPH_CFLAGS $VORBIS_CFLAGS $OGG_CFLAGS"

# Build shout-config
SHOUT_VERSION="$VERSION"
SHOUT_LIBS="-lshout"
SHOUT_LIBDEPS="$VORBIS_LIBS $OGG_LIBS $PTHREAD_LIBS"
SHOUT_CFLAGS="$VORBIS_CFLAGS $OGG_CFLAGS $PTHREAD_CFLAGS"
AC_CONFIG_LIBCONFIG_IN_STATIC
AC_CONFIG_LIBCONFIG_IN([shout])

dnl Make substitutions

AC_SUBST(LIBTOOL_DEPS)
AC_SUBST(OPT)
AC_SUBST(LIBS)
AC_SUBST(DEBUG)
AC_SUBST(CFLAGS)
AC_SUBST(PROFILE)
AC_SUBST(XIPH_CFLAGS)

AC_OUTPUT([Makefile include/Makefile include/shout/Makefile src/Makefile src/net/Makefile
  src/timing/Makefile src/thread/Makefile src/avl/Makefile src/httpp/Makefile examples/Makefile
  shout-config])
